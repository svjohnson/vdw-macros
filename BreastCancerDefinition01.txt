%macro BreastCancerDefinition01(StartDt = 01Jan1997
                              , EndDt = 31Dec2003
                              , OutSet = brca
                              , TumDat = vdw.tumor) ;
   /*
      Pulls the set of "incident" (that is, first-ocurring during the specified date range)
      breast cancers, both invasive and in-situ (but excluding LCIS).

      These criteria are based on the ones used for the Early Screening study.
      See: https://www.kpchr.org/CRN2/apps/storage/docs/20000823whmesprogramming_case_criteria.doc.
   */

   proc sql number ;
      create table AllBreastTumors as
      select mrn
            , DxDate
            , DtMrk1 as ERMarker
            , StageGen
            , StageAJ
      from &TumDat
      where DxDate between "&StartDt"d and "&EndDt"d  AND
            ICDOSite between 'C500' and 'C509'        AND
            Gender = '2'                              AND
            Morph NOT between '9590' and '9979'       AND
            ( (behav in ('3', '6', '9')) OR
              (behav = '2' AND MORPH ne '8520')) ;

      create table FirstBTs as
      select DISTINCT b.*
      from  AllBreastTumors as b INNER JOIN
            (select MRN, min(DxDate) as FirstBTDate from AllBreastTumors group by MRN) as b2
      on    b.MRN = b2.MRN AND
            b.DxDate = b2.FirstBTDate
      order by mrn, ERMarker ;

      title "These people had >1 tumor dxd on the same day, each with different receptor statuses or stages." ;
      select *
      from  FirstBTs
      where MRN in (select MRN from FirstBTs group by MRN having count(*) > 1) ;
      title ;

      * Some women will have > 1 breast tumor dxd on the same day, each with ;
      * different stages and/or receptor statuses.  We want to call the receptor status ;
      * positive if any tumor is positive, and we want to take the greatest stage. ;

      create table &OutSet as
      select mrn
            , dxdate
            , min(case ERMarker
                     when '0' then 10
                     else input(ERMarker, 2.0)
                  end) as ERMarker format = ERM.
            , max(case lowcase(StageGen)
                     when '9' then -1
                     when 'b' then -2
                     else input(StageGen, 2.0)
                  end) as StageGen format = StageGen.
            , max(case lowcase(StageAJ)
                     when '' then -1
                     when 'unk' then -1
                     when '2a' then 2
                     when '2b' then 2.5
                     when '3a' then 3
                     when '3b' then 3.5
                     else input(StageAJ, 2.0)
                  end) as StageAJ format = StageAJ.
      from FirstBTs
      group by mrn, dxdate ;
   quit ;

%mend BreastCancerDefinition01 ;