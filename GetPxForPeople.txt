%macro GetPxForPeople(People     /* The name of a dataset containing the people whose procedures you want. */
                    , StartDt    /* The date on which you want to start collecting procedures. */
                    , EndDt      /* The date on which you want to stop collecting procedures. */
                    , Outset     /* The name of the output dataset containing the procedures. */) ;

   /*
      Gets the procedures for a specified set of people (identified by MRNs)
      which ocurred between the dates specified in StartDt and EndDt.
   */

   %if &People = &Outset %then %do ;
      %put PROBLEM: The People dataset must be different from the OutSet dataset. ;
      %put PROBLEM: Both parameters are set to "&People". ;
      %put PROBLEM: Doing nothing. ;
   %end ;
   %else %do ;
      proc sql ;
         create table &OutSet as
         select r.*
         from util.&_PxData as r INNER JOIN
               &People as p
         on    r.MRN = p.MRN
         where r.ADate BETWEEN "&StartDt"d AND "&EndDt"d ;
      quit ;
   %end ;
%mend GetPxForPeople ;