%macro GetRxForPeople(People     /* The name of a dataset containing the people whose fills you want. */
                    , StartDt    /* The date on which you want to start collecting fills. */
                    , EndDt      /* The date on which you want to stop collecting fills. */
                    , Outset     /* The name of the output dataset containing the fills. */) ;

   /*
      Gets the pharmacy fills for a specified set of people (identified by MRNs)
      which ocurred between the dates specified in StartDt and EndDt.
   */

   %if &People = &Outset %then %do ;
      %put PROBLEM: The People dataset must be different from the OutSet dataset. ;
      %put PROBLEM: Both parameters are set to "&People". ;
      %put PROBLEM: Doing nothing. ;
   %end ;
   %else %do ;
      proc sql ;
         create table &OutSet as
         select r.*
         from rx.&RxSet as r INNER JOIN
               &People as p
         on    r.MRN = p.MRN
         where r.RxDate BETWEEN "&StartDt"d AND "&EndDt"d ;
      quit ;
   %end ;
%mend GetRxForPeople ;